//region Startup Plugins

apply plugin: "com.android.library"

//endregion

//region Android

android {

  //region Build Configuration

  buildTypes {
    debug {
      useProguard false
      minifyEnabled false
      shrinkResources false
    }
    release {
      useProguard true
      minifyEnabled true
      shrinkResources false
      proguardFiles getDefaultProguardFile("proguard-android.txt"),
          "proguard-rules.pro"
    }
  }

  //endregion

  //region Compile Options

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  compileSdkVersion deps.build_versions.compile_sdk

  //endregion

  //region Flavor Dimensions

  flavorDimensions "irrelevant"

  //endregion

  //region Default Config

  defaultConfig {
    minSdkVersion deps.build_versions.min_sdk
    targetSdkVersion deps.build_versions.target_sdk
    versionCode getAutoVersionCode()
    versionName getAutoVersionName()
    dimension "irrelevant"
  }

  //endregion

  //region Gradle Properties

  buildTypes.each {
    it.buildConfigField "Boolean", "ENABLE_DEBUG_BRIDGE", EnableDebugBridge
    it.buildConfigField "Boolean", "ENABLE_LEAK_CANARY", EnableLeakCanary
    it.buildConfigField "Boolean", "ENABLE_LOGGER", EnableLogger
    it.buildConfigField "Integer", "SERVER_CONNECTION_TIMEOUT", ServerConnectionTimeout
    it.buildConfigField "String", "SERVER_LOGGING_LEVEL", ServerLoggingLevel
    it.buildConfigField "Integer", "SERVER_READ_TIMEOUT", ServerReadTimeout
    it.buildConfigField "Integer", "SERVER_WRITE_TIMEOUT", ServerWriteTimeout
  }

  //endregion

  //region Product Flavors

  productFlavors {
    dev {
      // Do not remove empty configuration.
    }
    prod {
      // Do not remove empty configuration.
    }
  }

  //endregion
}

//endregion

//region Dependencies

dependencies {

  //region Application Files

  implementation fileTree(dir: "libs", include: ["*.jar"])

  //endregion

  //region Androidx

  api deps.androidx.annotation

  //endregion

  //region Debugging

  implementation deps.facebook.stetho
  implementation deps.misc.timber

  //endregion

  //region Dependency Injection

  api deps.dagger.runtime
  annotationProcessor deps.dagger.compiler
  compileOnly deps.misc.javax

  //endregion

  //region Miscellaneous

  api deps.misc.commons_lang
  api deps.misc.joda_time

  //endregion

  //region Reactive Extensions

  api deps.rx.android
  api deps.rx.java

  //endregion
}

//endregion
