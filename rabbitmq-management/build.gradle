buildscript {
  repositories {
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }

  dependencies {
    classpath 'me.tatarka:gradle-retrolambda:3.3.1'
    classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    classpath 'com.frogermcs.androiddevmetrics:androiddevmetrics-plugin:0.3.1'
    classpath 'com.google.gms:google-services:3.0.0'
  }
}

repositories {
  mavenCentral()
  maven { url "https://jitpack.io" }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.frogermcs.androiddevmetrics'

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.1"

  defaultConfig {
    applicationId "com.todev.rabbitmqmanagement"
    versionCode 1
    versionName "0.0.5"

    minSdkVersion 16
    targetSdkVersion 25

    multiDexEnabled true
  }

  buildTypes {
    debug {
      versionNameSuffix '-todev'
      debuggable true
      testCoverageEnabled true
    }

    release {
      minifyEnabled true
      shrinkResources true

      proguardFile rootProject.file('settings/proguard/dbflow.pro')
      proguardFile rootProject.file('settings/proguard/fragmentargs.pro')
      proguardFile rootProject.file('settings/proguard/icepick.pro')
      proguardFile rootProject.file('settings/proguard/lombok.pro')
      proguardFile rootProject.file('settings/proguard/okhttp3.pro')
      proguardFile rootProject.file('settings/proguard/okio.pro')
      proguardFile rootProject.file('settings/proguard/picasso.pro')
      proguardFile rootProject.file('settings/proguard/retrofit2.pro')
      proguardFile rootProject.file('settings/proguard/retrolambda.pro')
      proguardFile rootProject.file('settings/proguard/rx-java.pro')
      proguardFile rootProject.file('settings/proguard/streamsupport.pro')
      proguardFile rootProject.file('settings/proguard/support-design.pro')
      proguardFile rootProject.file('settings/proguard/support-v4.pro')
      proguardFile rootProject.file('settings/proguard/support-v7-appcompat.pro')
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  productFlavors {
    development {
      buildConfigField 'Boolean', 'IS_DEVELOPMENT', 'true'
    }

    production {
      buildConfigField 'Boolean', 'IS_DEVELOPMENT', 'false'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/license.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/notice.txt'
  }
}

ext.supportVersion = '25.0.1'
ext.rxVersion = '2.0.1'
ext.daggerVersion = '2.7'
ext.butterknifeVersion = '8.4.0'
ext.icepickVersion = '3.2.0'
ext.intentbuilderVersion = '0.14.0'
ext.fragmentargsVersion = '3.0.2'
ext.dbflowVersion = '4.0.0-beta2'
ext.retrofitVersion = '2.1.0'
ext.stethoVersion = '1.4.1'

dependencies {
  compile "com.android.support:appcompat-v7:$supportVersion"
  compile "com.android.support:design:$supportVersion"
  compile "com.android.support:recyclerview-v7:$supportVersion"
  compile "com.android.support:cardview-v7:$supportVersion"
  compile 'com.android.support:multidex:1.0.1'

  // Reactive extensions.
  compile "io.reactivex.rxjava2:rxandroid:$rxVersion"
  compile "io.reactivex.rxjava2:rxjava:$rxVersion"

  // Support.
  provided 'org.projectlombok:lombok:1.16.10'
  compile 'net.sourceforge.streamsupport:streamsupport:1.5.2'

  // Dependency injection.
  compile "com.google.dagger:dagger:$daggerVersion"
  annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

  // View injection.
  compile "com.jakewharton:butterknife:$butterknifeVersion"
  annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

  // Persistence of instance states.
  compile "frankiesardo:icepick:$icepickVersion"
  provided "frankiesardo:icepick-processor:$icepickVersion"

  // Intents handling.
  compile "se.emilsjolander:intentbuilder-api:$intentbuilderVersion"
  annotationProcessor "se.emilsjolander:intentbuilder-compiler:$intentbuilderVersion"

  // Fragments handling.
  compile "com.hannesdorfmann.fragmentargs:annotation:$fragmentargsVersion"
  annotationProcessor "com.hannesdorfmann.fragmentargs:processor:$fragmentargsVersion"

  // Database ORM.
  compile "com.github.Raizlabs.DBFlow:dbflow-core:$dbflowVersion"
  compile "com.github.Raizlabs.DBFlow:dbflow:$dbflowVersion"
  annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:$dbflowVersion"

  // Networking.
  compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
  compile "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
  compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
  compile 'com.squareup.picasso:picasso:2.5.2'

  // Time handling.
  compile 'joda-time:joda-time:2.9.6'

  // Charts.
  compile "com.github.philjay:mpandroidchart:v3.0.0-beta1"

  // Memory leak detection.
  compile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

  // Logger.
  compile 'com.jakewharton.timber:timber:4.3.1'

  // -- Development flavor only. --

  // FPS measurement.
  developmentCompile 'jp.wasabeef:takt:1.0.3'

  // Browser debugging.
  developmentCompile "com.facebook.stetho:stetho:$stethoVersion"
  developmentCompile "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

  // Memory leak detection
  developmentCompile 'com.squareup.leakcanary:leakcanary-android:1.5'

  // -- Test flavor only. --

  // Unit tests.
  testCompile 'junit:junit:4.12'
  testCompile 'com.google.truth:truth:0.30'
  testCompile 'org.mockito:mockito-core:2.2.16'
  testCompile 'org.robolectric:robolectric:3.1.4'
  testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
}
